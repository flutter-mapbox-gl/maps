import org.gradle.internal.os.OperatingSystem

group 'com.mapbox.mapboxgl'
version '1.1-SNAPSHOT'

def getTokenFromEnv() {
    def token = System.getenv('SDK_REGISTRY_TOKEN');
    if (token == null || token.empty) {
        throw new Exception("SDK Registry token is null. See README.md for more information.")
    }
    return token;
}

def getTokenFromNetrc() {
    def contentList = [:]
    def netrcPath = System.getProperty("user.home") + '/.netrc';
    def content = new File(netrcPath);
    def lastMachine = null;

    if (!content.exists()) {
        return null;
    }
    content.eachLine {
        it ->
            it.split().collect {
                it.trim();
            }.with {
                if (it.size() > 1) {
                    switch (it[0]) {
                        case 'machine':
                            lastMachine = it[1];
                            contentList.put(lastMachine, [:]);
                            break
                        default:
                            contentList.
                            "$lastMachine".put(it[0], it[1])
                    }
                }
            };
    }
    return contentList["api.mapbox.com"]["password"];
}

def getToken() {
    if (OperatingSystem.current().isMacOsX()) {
        final netrcToken = getTokenFromNetrc();
        if (netrcToken != null) {
            return netrcToken;
        } else {
            return getTokenFromEnv();
        }
    } else {
        return getTokenFromEnv();
    }
}

def token = getToken();


buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
    }
}

rootProject.allprojects {

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = token
            }
        }
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 20
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dependencies {
        implementation "com.mapbox.mapboxsdk:mapbox-android-sdk:9.6.0"
        implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.9.0"
        implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v9:0.12.0"
        implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-offline-v9:0.7.0'
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}
